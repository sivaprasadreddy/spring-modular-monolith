plugins {
	id "java"
	id "org.springframework.boot" version "3.4.0"
	id "io.spring.dependency-management" version "1.1.6"
	id "com.diffplug.spotless" version "6.25.0"
	id "org.openrewrite.rewrite" version "6.28.3"
}

group = "com.sivalabs"
version = "0.0.1-SNAPSHOT"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
}

ext {
	set("springModulithVersion", "1.3.0")
	set("datasourceMicrometerSpringBootVersion", "1.0.6")
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	runtimeOnly "io.micrometer:micrometer-registry-prometheus"
	//implementation "io.micrometer:micrometer-tracing-bridge-brave"
	//implementation "io.zipkin.reporter2:zipkin-reporter-brave"
	implementation "io.micrometer:micrometer-tracing-bridge-otel"
	implementation "io.opentelemetry:opentelemetry-exporter-zipkin"
	implementation "net.ttddyy.observation:datasource-micrometer-spring-boot:${datasourceMicrometerSpringBootVersion}"

	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.flywaydb:flyway-core"
	implementation "org.flywaydb:flyway-database-postgresql"
	runtimeOnly "org.postgresql:postgresql"

	implementation "org.springframework.boot:spring-boot-starter-amqp"
	implementation "org.springframework.modulith:spring-modulith-starter-core"
	implementation "org.springframework.modulith:spring-modulith-starter-jdbc"
	implementation "org.springframework.modulith:spring-modulith-events-amqp"
	runtimeOnly "org.springframework.modulith:spring-modulith-actuator"
	runtimeOnly "org.springframework.modulith:spring-modulith-observability"

	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
	implementation "nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect"
	implementation "io.github.wimdeblauwe:htmx-spring-boot-thymeleaf:4.0.1"
	implementation "org.webjars:bootstrap:5.3.3"
	implementation "org.webjars.npm:htmx.org:2.0.4"

	developmentOnly "org.springframework.boot:spring-boot-docker-compose"
	developmentOnly "org.springframework.boot:spring-boot-devtools"
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	rewrite "org.openrewrite.recipe:rewrite-spring:5.24.1"
	rewrite "org.openrewrite.recipe:rewrite-testing-frameworks:2.23.1"
	rewrite "org.openrewrite.recipe:rewrite-static-analysis:1.21.1"

	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.amqp:spring-rabbit-test"
	testImplementation "org.springframework.modulith:spring-modulith-starter-test"
	testImplementation "org.springframework.boot:spring-boot-testcontainers"
	testImplementation "org.testcontainers:junit-jupiter"
	testImplementation "org.testcontainers:postgresql"
	testImplementation "org.testcontainers:rabbitmq"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	testImplementation "org.hamcrest:hamcrest"

}

dependencyManagement {
	imports {
		mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
	}
}

tasks.named("test") {
	useJUnitPlatform()
	testLogging {
		events = ["PASSED", "FAILED", "SKIPPED"]
		showStandardStreams = true
		exceptionFormat = "FULL"
	}
}

rewrite {
	setExportDatatables(true)
	activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_3")
	activeRecipe("org.openrewrite.java.testing.junit5.JUnit5BestPractices")
	activeRecipe("org.openrewrite.java.testing.testcontainers.TestContainersBestPractices")
	activeRecipe("org.openrewrite.staticanalysis.CodeCleanup")
	activeRecipe("org.openrewrite.staticanalysis.CommonStaticAnalysis")
}

spotless {
	java {
		target("src/*/java/**/*.java")
		importOrder()
		removeUnusedImports()
		palantirJavaFormat("2.50.0")
		formatAnnotations()
	}
}

springBoot {
	buildInfo()
}

tasks.named("bootBuildImage") {
	imageName = "sivaprasadreddy/${project.name}"
}
